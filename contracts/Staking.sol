// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

// Imports
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/access/AccessControl.sol";
import "./interfaces/IStaking.sol";

contract Staking is AccessControl, IStaking {
  // Structures
  struct StakePosition {
    uint amount; // amount of tokens staked
    uint timestamp; // block timestamp of the stake operation
  }

  // Constants
  uint public constant REWARDS_DIVIDER = 10000;
  uint public immutable REWARDS_PERCENTAGE;
  uint public immutable STAKING_TERM;

  // Members
  IERC20 public tokenToStake;
  IERC20 public rewardToken;
  uint public availableRewards;
  mapping (address => uint) public tail;
  mapping (address => uint) public head;
  mapping (address => mapping(uint => StakePosition) ) public stakeQueue;

  /**
   * @notice Constructor
   * @param _tokenToStake Address of the staking token
   * @param _rewardToken Address of the reward token
   * @param _rewardsPercentage Percentage of token to distribute as a reward
            for the stakers
   * @param _stakingTerm Seconds that needs to pass from the stake to consider
            the position eligible for rewards
   */
  constructor (
    address _tokenToStake, 
    address _rewardToken,  
    uint _rewardsPercentage, 
    uint _stakingTerm
  ) {
    // Setup roles
    _setupRole(DEFAULT_ADMIN_ROLE, msg.sender);

    tokenToStake = IERC20(_tokenToStake);
    rewardToken = IERC20(_rewardToken);

    REWARDS_PERCENTAGE = _rewardsPercentage;
    STAKING_TERM = _stakingTerm;
  }

  /////////////////////////////////////////////////////////////////////////////
  // EXTERNAL FUNCTIONS

  /**
   * @notice Stake tokens
   * @param _amount Amount of tokens to stake
   * @dev Caller has to approve the transger of _amount staking tokens from
            its wallet to this contract
   */
  function stake(
    uint _amount
  ) external override {
    // Get tokens
    tokenToStake.transferFrom(msg.sender, address(this), _amount);

    // Create staking position
    uint positionIndex = head[msg.sender];

    StakePosition storage position = stakeQueue[msg.sender][positionIndex];
    position.amount = _amount;
    position.timestamp = block.timestamp;

    // Update head
    head[msg.sender] = positionIndex + 1;

    // emit event
    emit Stake(msg.sender, _amount, block.timestamp);
  }

  /**
   * @notice Unstake positions that are eligible for the rewards
   */
  function unstake() external override {
    _unstake(msg.sender, false);
  }

  /**
   * @notice Unstake all positions (also ones not eligible for the rewards)
   */
  function unstakeForced() external override {
    _unstake(msg.sender, true);
  }

  /**
   * @notice Compute staking rewards for an address
   * @param _staker Address of which you want to compute rewards
   */
  function computeRewards(address _staker) external view override returns (uint) {
    (uint rewards , , ) = _rewards(_staker, false);

    return rewards;
  }

  /////////////////////////////////////////////////////////////////////////////
  // PRIVILEGED FUNCTIONS
  /**
   * @notice Add rewards to the staking
   * @param _amount Amount of rewards to add
   * @dev Caller has to approve the transfer of _amount reward tokens from its
            wallet to this contract
   */
  function addRewards(
    uint _amount
  ) external override onlyRole(DEFAULT_ADMIN_ROLE) {
    // Get tokens 
    rewardToken.transferFrom(msg.sender, address(this), _amount);

    // Update counter
    availableRewards += _amount;
  }

  /////////////////////////////////////////////////////////////////////////////
  // INTERNAL FUNCTIONS
  /**
   * @notice Compute rewards and staked tokens for an address
   * @param _staker Address of the staker
   * @param _forced If true, counts staked tokens for every position. If not,
            only checks for rewards eligible positions
   * @return totalRewards Rewards generated by the stakers
   * @return totalStakedTokens Number of staked tokens by the staker
   * @return tailPosition New tail position if the staker effectively unstake
   */
  function _rewards(
    address _staker,
    bool _forced
  ) internal view returns (uint, uint, uint) {
    uint totalRewards = 0;
    uint totalStakedTokens = 0;

    // Compute total rewards 
    uint tailPosition = tail[_staker];
    for (; tailPosition < head[_staker]; tailPosition++) {
      StakePosition memory position = stakeQueue[_staker][tailPosition];  

      // Compute time difference and check if stake is eligible 
      uint timeDiff = block.timestamp - position.timestamp;
      if (timeDiff >= STAKING_TERM) {
        // Since it's eligible, add rewards
        totalRewards += position.amount * REWARDS_PERCENTAGE / REWARDS_DIVIDER;
      } else if (! _forced) {
        break;
      }

      // Update staked tokens to transfer
      totalStakedTokens += position.amount;
    }

    // Sanity check. Do not transfer more than what's in the contract
    if (totalRewards > availableRewards) {
      totalRewards = availableRewards;
    }

    return (totalRewards, totalStakedTokens, tailPosition);
  }

  /**
   * @notice Internal unstake
   * @param _staker Address of the staker
   * @param _forceUnstake If true, unstake every position (also the ones that 
            won't receive rewards)
   * @dev Transfer accumulated rewards and also staked tokens 
   */
  function _unstake(
    address _staker, 
    bool _forceUnstake
  ) internal {
    (uint totalRewards, uint totalStakedTokens, uint tailPosition) = _rewards(_staker, _forceUnstake);

    // Transfer rewards
    if (totalRewards > 0) {
      rewardToken.transfer(_staker, totalRewards);
    }

    // Transfer staked tokens
    if (totalStakedTokens > 0) {
      tokenToStake.transfer(_staker, totalStakedTokens);
    }

    // Update available rewards
    availableRewards -= totalRewards;

    // Update tail
    tail[msg.sender] = tailPosition;

    // Emit event
    emit Unstake(_staker, totalRewards, block.timestamp);
  }
}
